#!/bin/csh
###############################################################################
#
# FILE: atten_pcal
#
#    Set the OTT pcal attenuation.
#
#    This command is called from the field system.
#
# HISTORY
#
# who          when           what
# ---------    -----------    ---------------------------------------
# lerner       18 Oct 2017    Original version
#
###############################################################################

#
#   Set up some important variables
#

set telescope_file = /tmp/telescope_choice

#
#   Check that we are running on a supported operating system
#

if ( `uname` != "Linux" ) then
   echo "Error: unsupported architecture `uname`\!"
   exit 1
endif

#
#   Check that we are running on a supported FS-computer
#

if ( `hostname` != "freja" && `hostname` != "fulla" ) then
   echo "Error: non-allowed FS-computer '`hostname`'\!"
   exit 1
endif

#
#   Check which OTT we are running on
#

if ( ! -r $telescope_file ) then
   echo "FAKING $*"
   exit 0
endif

if ( `cut -c 1-5 $telescope_file` == "OTT-1" ) then
   set pcal_ip = 192.165.6.76
else if ( `cut -c 1-5 $telescope_file` == "OTT-2" ) then
   set pcal_ip = 192.165.6.77
else
   echo "FAKING $*"
   exit 0
endif

set pcal_port = 5000

#
#   Check that we got the right number of arguments
#

if ( $#argv != 1 ) then
   echo "Error: bad number of arguments given to 'atten_pcal'\!"
   echo "Usage: atten_pcal 0.0-31.5   (steps of 0.5)"
   exit 1
endif

#
#   Check the argument and build the command
#

if ( "$1" =~ [0-9] || "$1" =~ [1-3][0-9] || "$1" =~ [0-9].[05] ||   \
     "$1" =~ [1-3][0-9].[05] ) then
   set command = "atten_pcal=$1"
else
   echo "Error: bad argument given to 'atten_pcal'\!"
   echo "Usage: atten_pcal 0.0-31.5   (steps of 0.5)"
   exit 1
endif

#
#   Send the command
#

nc -w 1 $pcal_ip $pcal_port << EOF
$command
EOF

set code = $status

#
#   Log the command and its result
#

if ( $code == 0 ) then
   echo "Set $0:t $*"
else
   echo "ERROR: failed to set $0:t $*\!\!\!"
endif

#
#   We are finished
#

exit $code
